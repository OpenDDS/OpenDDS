// -*- C++ -*-
// $Id$
#ifndef TAO_DDS_DCPSINFO_IDL
#define TAO_DDS_DCPSINFO_IDL

#include "dds/DdsDcpsInfrastructure.idl"
#include "dds/DdsDcpsPublication.idl"
#include "dds/DdsDcpsSubscription.idl"

#include "dds/DdsDcpsInfoUtils.idl"

#include "dds/DdsDcpsDataWriterRemote.idl"
#include "dds/DdsDcpsDataReaderRemote.idl"

module OpenDDS {
  module DCPS {
    interface DCPSInfo {


      // Domain participant calls to notify of a new topic
      TopicStatus assert_topic (out RepoId topicId,
                                in ::DDS::DomainId_t domainId,
                                in RepoId  participantId,
                                in string          topicName,
                                in string          DataTypeName,
                                in ::DDS::TopicQos   qos)
        raises (Invalid_Domain,
                Invalid_Participant);


      // Domain participant calls to locate a topic
      // Arbitrarily returns a topic of this topicName
      TopicStatus find_topic (in ::DDS::DomainId_t domainId,
                              in string          topicName,
                              out string         DataTypeName,
                              out ::DDS::TopicQos  qos,
                              out RepoId topicId)
        raises (Invalid_Domain);


      // Domain participant calls to remove a topic
      TopicStatus remove_topic (in ::DDS::DomainId_t domainId,
                                        in RepoId  participantId,
                                        in RepoId  topicId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Topic);


      // Topic entity calls to enable a topic
      TopicStatus enable_topic (in ::DDS::DomainId_t domainId,
                                in RepoId  participantId,
                                in RepoId  topicId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Topic);



      // publisher calls to create new publication
      // returns the id of the added publication
      // 0 is an invalid id
      RepoId add_publication (in ::DDS::DomainId_t domainId,
                              in RepoId participantId,
                              in RepoId topicId,
                              in DataWriterRemote publication,
    //                          in ::DDS::DataWriter publication,
                              in ::DDS::DataWriterQos qos,
                              in TransportInterfaceInfo transInfo,
                              in ::DDS::PublisherQos publisherQos)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Topic);


      // publisher calls to remove a publication
      void remove_publication (in ::DDS::DomainId_t domainId,
                               in RepoId participantId,
                               in RepoId publicationId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Publication);



      // subscriber calls to create new subscription
      // returns the id of the added subscription
      // 0 is an invalid id
      RepoId add_subscription (in ::DDS::DomainId_t domainId,
                               in RepoId participantId,
                               in RepoId topicId,
                               in DataReaderRemote subscription,
    //                            in ::DDS::DataReader subscription,
                               in ::DDS::DataReaderQos qos,
                               in TransportInterfaceInfo transInfo,
                               in ::DDS::SubscriberQos subscriberQos)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Topic);


      // subscriber calls to remove a subscription
      void remove_subscription (in ::DDS::DomainId_t domainId,
                                in RepoId participantId,
                                in RepoId subscriptionId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Subscription);



      // DomainParticipantFactory calls to add a new domain participant
      // returns the id of the added participant
      // 0 is an invalid id
      RepoId add_domain_participant (in ::DDS::DomainId_t domain,
                                     in ::DDS::DomainParticipantQos qos)
        raises (Invalid_Domain);


      // DomainParticipantFactory calls to remove a domain participant
      void remove_domain_participant (in ::DDS::DomainId_t domainId,
                                      in RepoId participantId)
        raises (Invalid_Domain,
                Invalid_Participant);



      //Note: the ignore_xxx methods are not reversable - per DDS spec.

      // ignore participant when matching/associating pubs & subs
      void ignore_domain_participant (in ::DDS::DomainId_t domainId,
                                      in RepoId myParticipantId,
                                      in RepoId ignoreParticipantId)
        raises (Invalid_Domain,
                Invalid_Participant);


      // ignore topic when matching/associating pubs & subs
      void ignore_topic (in ::DDS::DomainId_t domainId,
                         in RepoId myParticipantId,
                         in RepoId topicId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Topic);


      // ignore subscription when matching/associating pubs & subs
      void ignore_subscription (in ::DDS::DomainId_t domainId,
                                in RepoId myParticipantId,
                                in RepoId subscriptionId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Subscription);


      // ignore publication when matching/associating pubs & subs
      void ignore_publication (in ::DDS::DomainId_t domainId,
                               in RepoId myParticipantId,
                               in RepoId publicationId)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Publication);

      //=======================================================
      // Update the QoS of an enitity to support Built-in Topics
      // and updating the other side of assocations.
      // The caller will have already checked that the QoS
      // values are valid, consistent and changeable.
      // The repo will break any assocations that become
      // incompatible.
      //
      void update_domain_participant_qos (in ::DDS::DomainId_t domain,
                                          in RepoId participantId,
                                          in ::DDS::DomainParticipantQos qos)
        raises (Invalid_Domain,
                Invalid_Participant);

      void update_topic_qos (in RepoId topicId,
                             in ::DDS::DomainId_t domainId,
                             in RepoId  participantId,
                             in ::DDS::TopicQos   qos)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Topic);

      void update_publication_qos (in ::DDS::DomainId_t domainId,
                                   in RepoId participantId,
                                   in RepoId publicationId,
                                   in ::DDS::DataWriterQos qos,
                                   in ::DDS::PublisherQos publisherQos)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Publication);

      void update_subscription_qos (in ::DDS::DomainId_t domainId,
                                    in RepoId participantId,
                                    in RepoId subscriptionId,
                                    in ::DDS::DataReaderQos qos,
                                    in ::DDS::SubscriberQos subscriberQos)
        raises (Invalid_Domain,
                Invalid_Participant,
                Invalid_Subscription);


      //====== support built-in Topics
      // TBD for AAC - see design/DCPS/built_in_topics.txt
    };

  }; // module DDS
}; // module OpenDDS

#endif // TAO_DDS_DCPSINFO_IDL
