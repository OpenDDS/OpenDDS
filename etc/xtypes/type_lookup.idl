#include "RtpsRpc.idl"
#include "opendds_typeobject.idl"

// As per chapter 7.6.3.3.3 of XTypes spec
// Issue DDSXTY14-30:
// - These use XCDR2 and default to appendable.
// - The @RPCRequestType/@RPCReplyType structs are final.

module OpenDDS {
module XTypes {    // spec: dds::builtin

// computed from @hashid("getTypes")
const unsigned long TypeLookup_getTypes_HashId = 0x018252d3;
// computed from @hashid("getDependencies");
const unsigned long TypeLookup_getDependencies_HashId = 0x05aafb31;

// Query the TypeObjects associated with one or more TypeIdentifiers
@extensibility(MUTABLE)
struct TypeLookup_getTypes_In {
  @hashid TypeIdentifierSeq type_ids;
};

@extensibility(MUTABLE)
struct TypeLookup_getTypes_Out {
  @hashid TypeIdentifierTypeObjectPairSeq types;
  @hashid TypeIdentifierPairSeq complete_to_minimal;
};

union TypeLookup_getTypes_Result switch(long) {
  case DDS::RETCODE_OK:
    TypeLookup_getTypes_Out result;
};

// Query TypeIdentifiers that the specified types depend on
typedef sequence<octet, 32> OctetSeq32;

@extensibility(MUTABLE)
struct TypeLookup_getTypeDependencies_In {
  @hashid TypeIdentifierSeq type_ids;
  @hashid OctetSeq32 continuation_point;
};

@extensibility(MUTABLE)
struct TypeLookup_getTypeDependencies_Out {
  @hashid TypeIdentifierWithSizeSeq dependent_typeids;
  @hashid OctetSeq32 continuation_point;
};

union TypeLookup_getTypeDependencies_Result switch(long){
  case DDS::RETCODE_OK:
    TypeLookup_getTypeDependencies_Out result;
};

// Service Request
union TypeLookup_Call switch(long) {
  case TypeLookup_getTypes_HashId:
    TypeLookup_getTypes_In getTypes;
  case TypeLookup_getDependencies_HashId:
    TypeLookup_getTypeDependencies_In getTypeDependencies;
};

//@RPCRequestType
@final
struct TypeLookup_Request {
  DDS::RPC::RequestHeader header;
  TypeLookup_Call data;
};

// Service Reply
union TypeLookup_Return switch(long) {
  case TypeLookup_getTypes_HashId:
    TypeLookup_getTypes_Result getType;
  case TypeLookup_getDependencies_HashId:
    TypeLookup_getTypeDependencies_Result getTypeDependencies;
};

//@RPCReplyType
@final
struct TypeLookup_Reply {
  DDS::RPC::RequestHeader header;
  TypeLookup_Return return;
};

};
};
