/*
 * $Id$
 *
 * Copyright 2009 Object Computing, Inc.
 *
 * Distributed under the OpenDDS License.
 * See: http://www.opendds.org/license.html
 */

#ifndef OPENDDS_DCPS_SUBSCRIPTION_EXT_IDL
#define OPENDDS_DCPS_SUBSCRIPTION_EXT_IDL

/// OpenDDS extensions to the DDS Subscription Module

#include "dds/DdsDcpsSubscription.idl"
#include "dds/DdsDcpsGuid.idl"

module OpenDDS
{

  module DCPS
  {

    struct DurabilityQosPolicyExt
    {
      /// A DataReader with always_get_history set to true will receive
      /// "historic" samples from durable writers regardless of the reader's
      /// DURABILITY.kind setting.  Does not impact RxO.  Not changeable.
      boolean always_get_history;
    };

    struct DataReaderQosExt
    {
      DurabilityQosPolicyExt durability;
    };

    local interface SubscriberExt : DDS::Subscriber
    {
      void get_default_datareader_qos_ext(inout DataReaderQosExt qos);

      DDS::DataReader create_opendds_datareader(
        in DDS::TopicDescription a_topic,
        in DDS::DataReaderQos qos,
        in DataReaderQosExt ext_qos,
        in DDS::DataReaderListener a_listener,
        in DDS::StatusMask mask);
    };

    struct SubscriptionLostStatus {
      ::DDS::InstanceHandleSeq publication_handles;
    };

    typedef SubscriptionLostStatus SubscriptionDisconnectedStatus;
    typedef SubscriptionLostStatus SubscriptionReconnectedStatus;

    struct BudgetExceededStatus {
      long             total_count;
      long             total_count_change;
      ::DDS::InstanceHandle_t last_instance_handle;
    };

    /// Collection of latency statistics for a single association.
    struct LatencyStatistics {
      GUID_t                  publication;
      unsigned long           n;
      double                  maximum;
      double                  minimum;
      double                  mean;
      double                  variance;
    };

    typedef sequence<LatencyStatistics> LatencyStatisticsSeq;

    local interface DataReaderListener : ::DDS::DataReaderListener {

      // Called when a connection failure has been detected
      // and there are still associations using the connection
      // after the configurable graceful_disconnected_period.
      void on_subscription_disconnected(
        in ::DDS::DataReader reader,
        in SubscriptionDisconnectedStatus status);

      // Called when a disconnected connection
      // has been reconnected.
      void on_subscription_reconnected(
        in ::DDS::DataReader reader,
        in SubscriptionReconnectedStatus status);

      // called when a connection is lost and hence one
      // or more associations from this subscription to
      // some publishers have been lost.
      // A connection is "lost" when the retry attempts
      // have been exhausted.
      void on_subscription_lost(
        in ::DDS::DataReader reader,
        in SubscriptionLostStatus status);

      // called when the connection object is cleaned up and
      // the reconnect thread exits.
      // This hook is added for testing the reconnect thread
      // leaking problem when the subscriber crashes.
      void on_connection_deleted(
        in ::DDS::DataReader reader);

      // Called when the latency budget has been exceeded.
      void on_budget_exceeded(
        in ::DDS::DataReader reader,
        in BudgetExceededStatus status);
    };

    local interface DataReaderEx : ::DDS::DataReader {
      /// Obtain a sequence of statistics summaries.
      void get_latency_stats( inout LatencyStatisticsSeq stats);

      /// Clear any intermediate statistical values.
      void reset_latency_stats();

      /// Statistics gathering enable state.
      attribute boolean statistics_enabled;
    };

  };

};

#endif /* OPENDDS_DCPS_SUBSCRIPTION_EXT_IDL */
