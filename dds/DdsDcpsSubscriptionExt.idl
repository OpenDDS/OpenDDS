/*
 *
 *
 * Distributed under the OpenDDS License.
 * See: http://www.opendds.org/license.html
 */

#ifndef OPENDDS_DDSDCPSSUBSCRIPTIONEXT_IDL
#define OPENDDS_DDSDCPSSUBSCRIPTIONEXT_IDL

// OpenDDS extensions to the DDS Subscription Module

#include <dds/DdsDcpsSubscription.idl>
#include <dds/DdsDcpsGuid.idl>

module OpenDDS
{

  module DCPS
  {

    struct SubscriptionLostStatus {
      ::DDS::InstanceHandleSeq publication_handles;
    };

    typedef SubscriptionLostStatus SubscriptionDisconnectedStatus;
    typedef SubscriptionLostStatus SubscriptionReconnectedStatus;

    struct BudgetExceededStatus {
      long             total_count;
      long             total_count_change;
      ::DDS::InstanceHandle_t last_instance_handle;
    };

    /// Collection of latency statistics for a single association.
    struct LatencyStatistics {
      GUID_t                  publication;
      unsigned long           n;
      double                  maximum;
      double                  minimum;
      double                  mean;
      double                  variance;
    };

    local interface DataReaderListener : ::DDS::DataReaderListener {

      /// Called when a connection failure has been detected
      /// and there are still associations using the connection
      /// after the configurable graceful_disconnected_period.
      /// When connection is gracefully disconnected, this callback
      /// is also triggered.
      void on_subscription_disconnected(
        in ::DDS::DataReader reader,
        in SubscriptionDisconnectedStatus status);

      /// Called when a disconnected connection
      /// has been reconnected.
      void on_subscription_reconnected(
        in ::DDS::DataReader reader,
        in SubscriptionReconnectedStatus status);

      /// Called when a connection is lost and hence one
      /// or more associations from this subscription to
      /// some publishers have been lost.
      /// A connection is "lost" when the retry attempts
      /// have been exhausted.
      void on_subscription_lost(
        in ::DDS::DataReader reader,
        in SubscriptionLostStatus status);

      /// Called when the latency budget has been exceeded.
      void on_budget_exceeded(
        in ::DDS::DataReader reader,
        in BudgetExceededStatus status);
    };

#ifndef OPENDDS_SAFETY_PROFILE
    typedef sequence<LatencyStatistics> LatencyStatisticsSeq;

    local interface DataReaderEx : ::DDS::DataReader {
      /// Obtain a sequence of statistics summaries.
      void get_latency_stats( inout LatencyStatisticsSeq stats);

      /// Clear any intermediate statistical values.
      void reset_latency_stats();

      /// Statistics gathering enable state.
      attribute boolean statistics_enabled;
    };
#else
    local interface DataReaderEx : ::DDS::DataReader { };
#endif
  };

};

#endif /* OPENDDS_DCPS_SUBSCRIPTION_EXT_IDL */
