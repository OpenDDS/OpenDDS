/*
 * Distributed under the OpenDDS License.
 * See: http://www.opendds.org/license.html
 */

#ifndef OPENDDS_DDSDCPSTYPESUPPORTEXT_IDL
#define OPENDDS_DDSDCPSTYPESUPPORTEXT_IDL

#include <dds/DdsDcpsTopic.idl>
#include <dds/DdsDcpsPublication.idl>
#include <dds/DdsDcpsSubscription.idl>

module OpenDDS {
  module DCPS {

    /**
    * The generated type-specific FooTypeSupport interfaces include encode_to_* and
    * decode_from_* operations that take an argument of type RepresentationFormat.
    * Use one of the derived interfaces of RepresentationFormat to specify the external
    * format type (JSON, CDR, etc.) and any details of the type conversion.
    */
    local interface RepresentationFormat {
    };

    /**
    * Create an instance by passing *CDR_DATA_REPRESENTATION to TypeSupport::make_format().
    */
    local interface CdrRepresentationFormat : RepresentationFormat {
    };

    /**
    * Create an instance by passing JSON_DATA_REPRESENTATION to TypeSupport::make_format().
    */
    local interface JsonRepresentationFormat : RepresentationFormat {
    };

    local interface TypeSupport : ::DDS::TypeSupport {

      /**
      * create and activate a FooDataWriter.
      * Implemented by FooTypeSupportImpl.
      * Publisher::create_datawriter() will set the topic, listener, qos and
      * other initialization so the implementation of this method
      * is as small and simple as possible.
      */
      ::DDS::DataWriter create_datawriter();

      /**
      * create and activate a FooDataReader.
      * Implemented by FooTypeSupportImpl.
      * Subscriber::create_datareader() will set the topic, listener, qos and
      * other initialization so the implementation of this method
      * is as small and simple as possible.
      */
      ::DDS::DataReader create_datareader();

  #ifndef OPENDDS_NO_MULTI_TOPIC
      ::DDS::DataReader create_multitopic_datareader();
  #endif

      boolean has_dcps_key();

      ::DDS::ReturnCode_t unregister_type(
          in ::DDS::DomainParticipant domain,
          in string type_name);

      /// Get allowed representations defined by IDL annotations
      void representations_allowed_by_type(
        inout ::DDS::DataRepresentationIdSeq seq);

      RepresentationFormat make_format(in ::DDS::DataRepresentationId_t representation);
    };
  };
};

#endif /* OPENDDS_DCPS_TYPE_SUPPORT_IDL */
