# Distributed under the OpenDDS License. See accompanying LICENSE
# file or http://www.opendds.org/license.html for details.

include(${CMAKE_CURRENT_LIST_DIR}/config.cmake)

if(NOT DEFINED OPENDDS_BUILT_IN_TOPICS)
  set(OPENDDS_BUILT_IN_TOPICS ON)
endif()

if(NOT DEFINED OPENDDS_OBJECT_MODEL_PROFILE)
  set(OPENDDS_OBJECT_MODEL_PROFILE ON)
endif()

if(NOT DEFINED OPENDDS_PERSISTENCE_PROFILE)
  set(OPENDDS_PERSISTENCE_PROFILE ON)
endif()

if(NOT DEFINED OPENDDS_OWNERSHIP_PROFILE)
  set(OPENDDS_OWNERSHIP_PROFILE ON)
endif()
if(NOT DEFINED OPENDDS_OWNERSHIP_KIND_EXCLUSIVE)
  set(OPENDDS_OWNERSHIP_KIND_EXCLUSIVE ${OPENDDS_OWNERSHIP_PROFILE})
endif()

if(NOT DEFINED OPENDDS_CONTENT_SUBSCRIPTION)
  set(OPENDDS_CONTENT_SUBSCRIPTION ON)
endif()
if(NOT DEFINED OPENDDS_CONTENT_FILTERED_TOPIC)
  set(OPENDDS_CONTENT_FILTERED_TOPIC ${OPENDDS_CONTENT_SUBSCRIPTION})
endif()
if(NOT DEFINED OPENDDS_MULTI_TOPIC)
  set(OPENDDS_MULTI_TOPIC ${OPENDDS_CONTENT_SUBSCRIPTION})
endif()
if(NOT DEFINED OPENDDS_QUERY_CONDITION)
  set(OPENDDS_QUERY_CONDITION ${OPENDDS_CONTENT_SUBSCRIPTION})
endif()

if(NOT DEFINED OPENDDS_SUPPRESS_ANYS)
  set(OPENDDS_SUPPRESS_ANYS ON)
endif()

# Make Sure CMake can use the Paths
file(TO_CMAKE_PATH "${OPENDDS_ACE}" OPENDDS_ACE)
file(TO_CMAKE_PATH "${OPENDDS_MPC}" OPENDDS_MPC)
file(TO_CMAKE_PATH "${OPENDDS_TAO}" OPENDDS_TAO)

option(OPENDDS_CMAKE_VERBOSE "Print verbose output when loading the OpenDDS Config Package" OFF)
option(OPENDDS_DEFAULT_NESTED "Require topic types to be declared explicitly" ON)
option(OPENDDS_FILENAME_ONLY_INCLUDES "No directory info in generated #includes." OFF)
option(OPENDDS_GLOBAL_GENERATION "" ON)
set(OPENDDS_DEFAULT_SCOPE "PRIVATE" CACHE STRING "Default scope for OPENDDS_TARGET_SOURCES")
set_property(CACHE OPENDDS_DEFAULT_SCOPE PROPERTY STRINGS "PUBLIC" "PRIVATE" "INTERFACE")
option(OPENDDS_ALWAYS_GENERATE_LIB_EXPORT_HEADER "Always generate an export header for libraries" OFF)

set(_OPENDDS_RELATIVE_SOURCE_ROOT "${CMAKE_CURRENT_LIST_DIR}/..")
set(_OPENDDS_RELATIVE_PREFIX_ROOT "${CMAKE_CURRENT_LIST_DIR}/../../..")

get_filename_component(_OPENDDS_RELATIVE_SOURCE_ROOT
  ${_OPENDDS_RELATIVE_SOURCE_ROOT} ABSOLUTE)

get_filename_component(_OPENDDS_RELATIVE_PREFIX_ROOT
  ${_OPENDDS_RELATIVE_PREFIX_ROOT} ABSOLUTE)

macro(_OPENDDS_RETURN_ERR msg)
  message(SEND_ERROR "${msg}")
  set(OPENDDS_FOUND FALSE)
  return()
endmacro()

if(NOT DEFINED DDS_ROOT)
  if(OPENDDS_USE_PREFIX_PATH)
    set(DDS_ROOT "${_OPENDDS_RELATIVE_PREFIX_ROOT}/share/dds")
    set(OPENDDS_INCLUDE_DIRS "${_OPENDDS_RELATIVE_PREFIX_ROOT}/include")
    set(OPENDDS_BIN_DIR "${_OPENDDS_RELATIVE_PREFIX_ROOT}/bin")
    set(OPENDDS_LIB_DIR "${_OPENDDS_RELATIVE_PREFIX_ROOT}/lib")

  else()
    set(DDS_ROOT ${_OPENDDS_RELATIVE_SOURCE_ROOT})
    set(OPENDDS_INCLUDE_DIRS "${DDS_ROOT}")
    set(OPENDDS_BIN_DIR "${DDS_ROOT}/bin")
    set(OPENDDS_LIB_DIR "${DDS_ROOT}/lib")
  endif()
endif()

if (NOT DEFINED ACE_ROOT)
  if(OPENDDS_USE_PREFIX_PATH)
    set(ACE_ROOT "${_OPENDDS_RELATIVE_PREFIX_ROOT}/share/ace")
    set(ACE_INCLUDE_DIRS "${_OPENDDS_RELATIVE_PREFIX_ROOT}/include")
    set(ACE_LIB_DIR "${_OPENDDS_RELATIVE_PREFIX_ROOT}/lib")

  elseif(OPENDDS_ACE)
    set(ACE_ROOT ${OPENDDS_ACE})
    set(ACE_INCLUDE_DIRS "${ACE_ROOT}")
    set(ACE_LIB_DIR "${ACE_ROOT}/lib")

  else()
    _OPENDDS_RETURN_ERR("Failed to locate ACE_ROOT")
  endif()

  set(ACE_BIN_DIR "${ACE_ROOT}/bin")
endif()

if (NOT DEFINED TAO_ROOT)
  if(OPENDDS_USE_PREFIX_PATH)
    set(TAO_ROOT "${_OPENDDS_RELATIVE_PREFIX_ROOT}/share/tao")
    set(TAO_INCLUDE_DIR "${_OPENDDS_RELATIVE_PREFIX_ROOT}/include")
    set(TAO_BIN_DIR "${_OPENDDS_RELATIVE_PREFIX_ROOT}/bin")
    set(TAO_LIB_DIR "${_OPENDDS_RELATIVE_PREFIX_ROOT}/lib")

  elseif(OPENDDS_TAO)
    set(TAO_ROOT "${OPENDDS_TAO}")
    set(TAO_INCLUDE_DIR "${OPENDDS_TAO}")
    set(TAO_BIN_DIR "${ACE_BIN_DIR}")
    set(TAO_LIB_DIR "${ACE_LIB_DIR}")

  else()
    _OPENDDS_RETURN_ERR("Failed to locate TAO_ROOT")
  endif()

  set(TAO_INCLUDE_DIRS
    "${TAO_INCLUDE_DIR}"
    "${TAO_INCLUDE_DIR}/orbsvcs"
  )
endif()

if(NOT DEFINED OPENDDS_STATIC)
  set(OPENDDS_STATIC OFF)
endif()
if(OPENDDS_STATIC)
  set(OPENDDS_LIBRARY_TYPE STATIC)
else()
  set(OPENDDS_LIBRARY_TYPE SHARED)
endif()
