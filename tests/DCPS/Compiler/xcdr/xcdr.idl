#include <tao/LongSeq.pidl>
#include <tao/ShortSeq.pidl>
#include <tao/OctetSeq.pidl>
#include <tao/StringSeq.pidl>

#define COMMON_FIELDS \
  short short_field; \
  long long_field; \
  octet octet_field; \
  long long long_long_field;

#define COMMON_BRANCHES \
  case E_SHORT_FIELD: \
    short short_field; \
  case E_LONG_FIELD: \
    long long_field; \
  case E_OCTET_FIELD: \
    octet octet_field; \
  case E_LONG_LONG_FIELD: \
    long long long_long_field;

@final
struct FinalStruct {
  COMMON_FIELDS
};

@appendable
struct AppendableStruct {
  COMMON_FIELDS
};

@mutable
struct MutableStruct {
  COMMON_FIELDS
};

enum UnionDisc {
  E_SHORT_FIELD,
  E_LONG_FIELD,
  E_OCTET_FIELD,
  E_LONG_LONG_FIELD,
  E_ADDITIONAL_FIELD
};

@mutable
union MutableUnion switch (UnionDisc) {
  COMMON_BRANCHES
};

@final
union FinalUnion switch(UnionDisc) {
  COMMON_BRANCHES
};

// Appendable Tests ==========================================================

typedef AppendableStruct NestedStruct;

@appendable
struct AppendableWithNestedStruct {
  NestedStruct nested;
  COMMON_FIELDS
};

@appendable
struct AdditionalFieldNestedStruct {
  COMMON_FIELDS
  long additional_field;
};

@appendable
struct AdditionalFieldAppendableStruct {
  AdditionalFieldNestedStruct nested;
  COMMON_FIELDS
  long additional_field;
};

@appendable
struct AppendableWithNestedStruct2 {
  string string_field;
  NestedStruct nested;
};

@appendable
union AppendableUnion switch (UnionDisc) {
  COMMON_BRANCHES
};

@appendable
union ModifiedAppendableUnion switch (UnionDisc) {
  COMMON_BRANCHES
case E_ADDITIONAL_FIELD:
  long additional_field;
};


// Mutable Tests =============================================================

@mutable
struct MutableStructWithExplicitIDs {
  @id(4) short short_field;
  @id(6) long long_field;
  @id(8) octet octet_field;
  @id(10) long long long_long_field;
};

@mutable
struct ReorderedMutableStruct {
  @id(6) long long_field;
  @id(10) long long long_long_field;
  @id(8) octet octet_field;
  @id(4) short short_field;
};

@mutable
struct AdditionalFieldMutableStruct {
  @id(6) long long_field;
  @id(1) long additional_field;
  @id(10) long long long_long_field;
  @id(8) octet octet_field;
  @id(4) short short_field;
};

@final
struct Bool3 {
  boolean a;
  boolean b;
  boolean c;
};

@final
struct Octet5 {
  octet a;
  octet b;
  octet c;
  octet d;
  octet e;
};

@final
struct Short3 {
  short x;
  short y;
  short z;
};

@final
struct Struct7 {
  Short3 s3;
  octet o;
};

@final
struct Long3 {
  long a;
  long b;
  long c;
};

@mutable
struct LengthCodeStruct { //LC Size
  @key
  @id(0) octet o;         // 0    1
  @id(1) short s;         // 1    2
  @id(2) long l;          // 2    4
  @id(3) long long ll;    // 3    8

  @id(4) Bool3 b3;        // 4    3
  @id(5) Octet5 o5;       // 4    5
  @key
  @id(6) Short3 s3;       // 4    6
  @id(7) Struct7 t7;      // 4    7
  @id(8) Long3 l3;        // 4   12

  @id(11) string<1> str1; // 4  4+1
  @id(12) string<2> str2; // 4  4+2
  @id(13) string<3> str3; // 4  4+3
  @id(14) string<4> str4; // 3  4+4
  @key
  @id(15) string<5> str5; // 4  4+5
};

typedef sequence<long long, 3> LongLong3S;
typedef sequence<short, 3> Short3S;

@mutable
struct LC567Struct {            //LC Size
  @id(0) sequence<octet, 3> o3; // 5    3
  @id(1) sequence<long, 3> l3;  // 6  4x3
  @id(2) LongLong3S ll3;        // 7  8x3
  @id(3) Short3S s3;            // 3    4 (2 elements)
  @id(4) string<4> str4;        // 3    4
  @id(5) string<5> str5;        // 5    5
  @id(6) sequence<long> ls;     // 6  4x2 (2 elements)
  @key
  @id(7) string<7> str7;        // 5    7
};

@mutable
union MutableUnionWithExplicitIDs switch (UnionDisc) {
case E_SHORT_FIELD:
  @id(4) short short_field;
case E_LONG_FIELD:
  @id(6) long long_field;
case E_OCTET_FIELD:
  @id(8) octet octet_field;
case E_LONG_LONG_FIELD:
  @id(10) long long long_long_field;
};

@mutable
union ModifiedMutableUnion switch (UnionDisc) {
case E_LONG_FIELD:
  @id(6) long long_field;
case E_SHORT_FIELD:
  @id(4) short short_field;
case E_ADDITIONAL_FIELD:
  @id(12) long additional_field;
};

typedef sequence<long> LongSeq;

@mutable
union NestedUnion switch(short) {
case 1:
  @id(1) short short_field;
case 2:
  @id(2) LongSeq sequence_field;
default:
  @id(3) string string_field;
};

// DHEADER is not generated for anonymous sequence of string,
// so make a name for it here
typedef sequence<string> StringSeq;

@mutable
struct MixedMutableStruct {
  @id(1) MutableStructWithExplicitIDs struct_nested;
  @id(2) sequence<short> sequence_field;
  @id(3) NestedUnion union_nested;
  @id(4) StringSeq sequence_field2;
};

@mutable
struct ModifiedMixedMutableStruct {
  @id(3) NestedUnion union_nested;
  @id(4) StringSeq sequence_field2;
  @id(1) MutableStructWithExplicitIDs struct_nested;
};

// Mixed Extensibility Tests =================================================

@final
struct NestingFinalStruct {
  string string_field;
  AppendableStruct appendable_nested;
  sequence<short> sequence_field;
  MutableStruct mutable_nested;
};

@appendable
struct NestingAppendableStruct {
  string string_field;
  MutableStruct mutable_nested;
  sequence<long> sequence_field;
  FinalStruct final_nested;
};

@mutable
struct NestingMutableStruct {
  string string_field;
  AppendableStruct appendable_nested;
  sequence<octet> sequence_field;
  FinalStruct final_nested;
};

// KeyOnly Serialization ======================================================

#undef COMMON_FIELDS
#define COMMON_FIELDS(KEY, TYPE, NAME) \
  KEY TYPE NAME ## _value; \
  KEY TYPE NAME ## _array_value[2]; \
  /* TODO(iguessthislldo): Sequences aren't supported. See XTYPE-152 \
  KEY sequence<TYPE> NAME ## _seq_value; \
  */

@final
struct BasicUnkeyedStruct {
  COMMON_FIELDS(, long, long)
  string string_value;
  long extra_value;
};

@final
struct BasicKeyedStruct {
  COMMON_FIELDS(@key, long, long)
  @key string string_value;
  long extra_value;
};

@final
union UnkeyedUnion switch (long) {
case 0:
  char short_value;
default:
  long default_value;
};

@final
union KeyedUnion switch (@key long) {
case 0:
  char short_value;
default:
  long default_value;
};

@final
struct ComplexUnkeyedStruct {
  COMMON_FIELDS(, BasicUnkeyedStruct, unkeyed_struct)
  COMMON_FIELDS(, BasicKeyedStruct, keyed_struct)
  COMMON_FIELDS(, UnkeyedUnion, unkeyed_union)
  COMMON_FIELDS(, KeyedUnion, keyed_union)
  long extra_value;
};

@final
struct ComplexKeyedStruct {
  COMMON_FIELDS(@key, BasicUnkeyedStruct, unkeyed_struct)
  COMMON_FIELDS(@key, BasicKeyedStruct, keyed_struct)
  // TODO(iguessthislldo): Mark as Key when supported. See XTYPE-147
  COMMON_FIELDS(, UnkeyedUnion, unkeyed_union)
  COMMON_FIELDS(@key, KeyedUnion, keyed_union)
  long extra_value;
};
