#include <tao/LongSeq.pidl>
#include <tao/ShortSeq.pidl>
#include <tao/StringSeq.pidl>

module Mod {

  enum MyEnum { one, two, three, four };

  struct BasicTypes {
    octet o;
    unsigned short us;
    short s;
    unsigned long ul;
    long l;
    unsigned long long ull;
    long long ll;
    float f;
    double d;
    long double ld;
    boolean b;
    char c;
    // Not all supported C++ compilers handle the code generated from this type.
    // wchar wc;
    string str;
    wstring wstr;
  };

  typedef sequence<short> MySeq;
  typedef sequence<CORBA::StringSeq> NestedSeq;

  union MyUnion switch (MyEnum) {
  case one:
    long a;
  case two:
    boolean b;
  case three:
    double d;
  case four:
    NestedSeq s;
  };

  typedef sequence<NestedSeq> NestedNestedSeq;
  typedef sequence<NestedNestedSeq> NestedNestedNestedSeq;

  typedef char CharArray[6];
  typedef string StringArray[4];

  @topic
  struct Sample {
    @key long id;
    string data;
    MyEnum enu;
    MyEnum enu2;
    BasicTypes bt;
    MySeq seq1;
    CORBA::LongSeq seq2;
    NestedSeq ns;
    MyUnion mu;
    CharArray ca;
    StringArray sa;
  };

  typedef sequence<MyUnion> MyUnionSeq;

  enum MyOtherEnum { small, medium, large };

  union MyOtherUnion switch (MyOtherEnum) {
  case small:
    string one;
  case medium:
    NestedSeq two;
  case large:
    Sample three;
  };

  typedef MyOtherUnion JunkType[2];
  typedef boolean MaskType[2];

  @topic
  struct MaskedJunk {
    JunkType junk;
    MaskType mask;
  };

};
