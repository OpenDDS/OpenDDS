module key_annotation {

  /*
   * Simplest Case of a Sample Struct with no Key
   * 0 Keys
   */
  @topic
  struct UnkeyedStruct {
    long value;
  };

  /*
   * Simple Case of a Sample Struct with One Simple Key
   * 1 Key
   *  - key (10)
   */
  @topic
  struct SimpleKeyStruct {
    @key @id(10) long key;
    long value;
  };

  /*
   * Case of a Nested Struct Key
   * 2 Keys
   *  - non_nested_key (20)
   *  - nested_key.key (21, 10)
   */
  @topic
  struct NestedKeyStruct {
    @key @id(20) long non_nested_key;
    @key @id(21) SimpleKeyStruct nested_key;
  };

  /*
   * Using a Typedef of a Struct as a Key
   * 2 Keys
   *  - a_key_value (30)
   *  - my_struct_typedef_key.key (31, 10)
   */
  typedef SimpleKeyStruct MyStructTypedef;
  @topic
  struct TypedefStructKeyStruct {
    @key @id(30) long a_key_value;
    @key @id(31) MyStructTypedef my_struct_typedef_key;
  };

  /*
   * Case of an Array of Primitives
   * 2 Keys in non-XTypes contexts
   *  - values[0]
   *  - values[1]
   * 1 key in XTypes contexts
   *  - values (40)
   */
  typedef long LongArray[2];
  @topic
  struct LongArrayStruct {
    @key @id(40) LongArray values;
  };

  /*
   * Case of an Array of Structures
   * 2 Keys in non-XTypes contexts
   *  - values[0].key
   *  - values[0].key
   * 1 key in XTypes contexts
   *  - values (50)
   */
  typedef SimpleKeyStruct SimpleKeyStructArray[2];
  @topic
  struct SimpleKeyArray {
    @key @id(50) SimpleKeyStructArray values;
  };

  /*
   * Case of a Unkeyed Standalone Topic Union
   * 0 Keys
   */
  @topic
  union UnkeyedUnion switch (long) {
  case 0:
    long a;
  case 5:
    char b;
  default:
    float c;
  };

  /*
   * Case of a Keyed Union
   * 1 Key
   *  - _d() (DISCRIMINATOR_ID)
   */
  @topic
  union KeyedUnion switch (@key long) {
  case 0:
    long a;
  case 5:
    char b;
  default:
    float c;
  };

  /*
   * Case of a Keyed Union Nested inside a Struct
   * 3 Keys
   *  - value._d() (60, DISCRIMINATOR_ID)
   *  - keyed_unkeyed_union._d() (62, DISCRIMINATOR_ID)
   *  - another_key (63)
   */
  @topic
  struct KeyedUnionStruct {
    @key @id(60) KeyedUnion value;
    @id(61) KeyedUnion unkeyed_unkeyed_union;
    @key @id(62) UnkeyedUnion keyed_unkeyed_union;
    @key @id(63) long another_key;
  };

  /*
   * Struct with Multidimensional Array Keys
   * 2*3 + 2*3*4 (30) Keys in non-XTypes contexts
   *  - array1[0][0]
   *  - array1[0][1]
   *   ...
   *  - array1[1][2]
   *  - array2[0][0][0]
   *  - array2[0][0][1]
   *   ...
   *  - array2[1][2][3]
   * 2 keys in XTypes contexts
   *  - array1 (70)
   *  - array2 (71)
   */
  typedef short A_2D_Array_t[2][3];
  typedef short A_3D_Array_t[2][3][4];
  @topic
  struct MultidimensionalArrayStruct {
    @key @id(70) A_2D_Array_t array1;
    @key @id(71) A_3D_Array_t array2;
  };

  /*
   * Implied Keys
   * If a nested struct marked as a key has no keys, all the fields are implied
   * to be keys.
   */
  module ImpliedKeys {

    struct NoKeysStruct {
      @id(80) short a;
      @id(81) long b;
      @id(82) float c;
    };

    struct OneKeyStruct {
      @key @id(90) short a;
      long b;
      float c;
    };

    /*
     * Basic Usage
     * 5 keys
     *  - nested_no_keys.a (100, 80)
     *  - nested_no_keys.b (100, 81)
     *  - nested_no_keys.c (100, 82)
     *  - nested_one_key.a (101, 90)
     *  - non_nested (102)
     */
    @topic
    struct StructA {
      @key @id(100) NoKeysStruct nested_no_keys;
      @key @id(101) OneKeyStruct nested_one_key;
      @key @id(102) long non_nested;
    };

    /*
     * Make Sure This Works Recursively
     * 6 keys
     *  - as_key.nested_no_keys.a (110, 100, 80)
     *  - as_key.nested_no_keys.b (110, 100, 81)
     *  - as_key.nested_no_keys.c (110, 100, 82)
     *  - as_key.nested_one_key.a (110, 101, 90)
     *  - as_key.non_nested (110, 102)
     *  - yet_another_key (111)
     */
    @topic
    struct StructB {
      @key @id(110) StructA as_key;
      @id(1) StructA not_as_key;
      @key @id(111) long yet_another_key;
    };

    struct StructWithKeyFalse {
      @id(120) short a;
      @id(1) @key(FALSE) long b;
      @id(121) float c;
    };

    /*
     * @key(FALSE) Amid Implied Keys
     * If there are fields that are marked with @key(FALSE) and none marked
     * with @key or @key(TRUE), then keys can still be implied, but it excludes
     * the fields that are marked with @key(FALSE).
     * 2 keys
     *  - as_key.a (130, 120)
     *  - as_key.c (130, 121)
     */
    @topic
    struct StructC {
      @key @id(130) StructWithKeyFalse as_key;
      StructWithKeyFalse not_as_key;
    };

  };

};
